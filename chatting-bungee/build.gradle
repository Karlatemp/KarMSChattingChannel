/*
 * Copyright (c) 2018-2020 Karlatemp. All rights reserved.
 * @author Karlatemp <karlatemp@vip.qq.com> <https://github.com/Karlatemp>
 * @create 2020/07/26 10:20:48
 *
 * KarMSChattingChannel/KarMSChattingChannel/build.gradle
 */

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
}

dependencies {
    def ktor_version = "1.3.1"

    // https://mvnrepository.com/artifact/org.jetbrains.kotlinx/kotlinx-coroutines-core
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.8'

    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-websockets-jvm:$ktor_version"
    implementation 'net.md-5:bungeecord-api:1.16-R0.3-SNAPSHOT'
    implementation project(':chatting-common')
}

shadowJar {
    dependencyFilter.exclude { dep ->
        switch (dep.moduleGroup) {
            case "io.netty":
            case "net.md-5":
                return true
        }
        switch ("${dep.moduleGroup}:${dep.moduleName}") {
            case "org.yaml:snakeyaml":
            case "se.llbit:jo-nbt":
            case "com.google.code.gson:gson":
            case "com.google.guava:guava":
            case "net.md-5:bungeecord-chat":
            case "commons-lang:commons-lang":
            case "org.spigotmc:spigot-api":
            case "org.slf4j:slf4j-api":
                return true
            default:
                println("${dep.moduleGroup}:${dep.moduleName}")
                return false
        }
    }
}
